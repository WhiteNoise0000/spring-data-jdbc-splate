plugins {
  id 'java'
  id 'org.springframework.boot' version '3.4.5'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'com.coditory.manifest' version '0.2.6'
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
  id 'io.github.danysk.publish-on-central' version '8.0.7'
}

group = 'io.github.whitenoise0000'
version = '0.1.1'
sourceCompatibility = '17'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
	mavenCentral()
}

dependencies {
  // 2-way SQLパーサー
  implementation 'com.github.mygreen:splate:0.3'
  implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  testCompileOnly 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'

  testImplementation 'com.h2database:h2:2.1.+'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
  withSourcesJar()
  withJavadocJar()
}

bootJar {
  // bootJarは作成しない
  enabled = false
}
jar {
  // plainを付けない
  archiveClassifier = ''
}

manifest {
  buildAttributes = false
}

tasks.named('test') {
	useJUnitPlatform()
}

publishOnCentral {
    projectDescription.set("splate(2Way-SQL library) wrapper for Spring Data JDBC")
    repository("https://github.com/WhiteNoise0000/spring-data-jdbc-splate")
    licenseName.set("Apache License, Version 2.0")
    licenseUrl.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
    developers = [
        developer {
            id.set("WhiteNoise0000")
        }
    ]
}