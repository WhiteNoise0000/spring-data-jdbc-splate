plugins {
  id 'java'
  id 'maven-publish'
  id 'org.springframework.boot' version '3.4.7'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'com.coditory.manifest' version '0.2.6'
  id 'cl.franciscosolis.sonatype-central-upload' version '1.0.3'
}

group = 'io.github.whitenoise0000'
version = '0.1.1'
sourceCompatibility = '17'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
	mavenCentral()
}

dependencies {
  // 2-way SQLパーサー
  implementation 'com.github.mygreen:splate:0.3'
  implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  testCompileOnly 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'

  testImplementation 'com.h2database:h2:2.1.+'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
  withSourcesJar()
  withJavadocJar()
}

bootJar {
  // bootJarは作成しない
  enabled = false
}
jar {
  // plainを付けない
  archiveClassifier = ''
}

manifest {
  buildAttributes = false
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = project.name
                description = "splate(2Way-SQL library) wrapper for Spring Data JDBC"
                url = "https://github.com/WhiteNoise0000/spring-data-jdbc-splate"
                licenses {
                    license {
                        name = "Apache License, Version 2.0"
                        url = "https://github.com/WhiteNoise0000/spring-data-jdbc-splate/blob/master/LICENSE"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "WhiteNoise0000"
                        name = "WhiteNoise0000"
                    }
                }
                scm {
                    url = "https://github.com/WhiteNoise0000/spring-data-jdbc-splate.git"
                }
            }
        }
    }
}

tasks.named("sonatypeCentralUpload") {
    // 公開するファイルを生成するタスクに依存する。
    dependsOn("jar", "sourcesJar", "javadocJar", "generatePomFileForMavenPublication")

    // Central Portalで生成したトークンを指定する。
    username = findProperty("mavenUsername")
    password = findProperty("mavenPassword")

    // タスク名から成果物を取得する。
    archives = files(
        tasks.named("jar"),
        tasks.named("sourcesJar"),
        tasks.named("javadocJar"),
    )
    // POMファイルをタスクの成果物から取得する。
    pom = file(
        tasks.named("generatePomFileForMavenPublication").get().outputs.files.single()
    )

    // PGPの秘密鍵を指定する。
    signingKey = findProperty("signingKey")
    // PGPの秘密鍵のパスフレーズを指定する。
    signingKeyPassphrase = findProperty("signingPassword")
}
