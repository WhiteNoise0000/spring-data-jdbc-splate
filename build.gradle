plugins {
  id 'java-library'
  id 'org.springframework.boot' version '3.4.7'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'com.vanniktech.maven.publish' version '0.34.0'
}

sourceCompatibility = '17'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
	mavenCentral()
}

dependencies {
  api 'com.github.mygreen:splate:0.3'
  api 'org.springframework.boot:spring-boot-starter-data-jdbc'

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  testCompileOnly 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'

  testImplementation 'com.h2database:h2:2.1.+'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
  enabled = false
}
jar {
  archiveClassifier = ''
}

tasks.named('test') {
	useJUnitPlatform()
}

mavenPublishing {
  coordinates('io.github.whitenoise0000', 'spring-data-jdbc-splate', '0.1.1')

  // POMファイルに記載される情報を設定
  pom {
    name = "spring-data-jdbc-splate"
    description = "splate(2Way-SQL library) wrapper for Spring Data JDBC"
    inceptionYear = "2023"
    url = "https://github.com/WhiteNoise0000/spring-data-jdbc-splate"
    licenses {
      license {
        name = "The Apache License, Version 2.0"
        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
      }
    }
    developers {
      developer {
        id = "WhiteNoise0000"
        name = "WhiteNoise0000"
        url = "https://github.com/WhiteNoise0000"
      }
    }
    scm {
      connection = "scm:git:git://github.com/WhiteNoise0000/spring-data-jdbc-splate.git"
      developerConnection = "scm:git:ssh://github.com/WhiteNoise0000/spring-data-jdbc-splate.git"
      url = "https://github.com/WhiteNoise0000/spring-data-jdbc-splate"
    }
  }

  // Maven Central (Sonatype) への公開を有効化
  publishToMavenCentral()

  // すべての成果物にPGP署名を行う
  signAllPublications()
}
